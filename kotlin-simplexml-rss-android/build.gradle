buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'digital.wup.android-maven-publish' version '3.1.1' apply false
    id "com.jfrog.bintray" //version "1.8.0"
    id "com.diffplug.gradle.spotless" version "3.7.0"
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

group 'com.github.magneticflux'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    lintOptions {
        abortOnError false
    }
}

afterEvaluate {
    apply plugin: 'digital.wup.android-maven-publish'
    publishing {
        publications {
            kotlinSimpleXmlRssAndroid(MavenPublication) {
                from components.android
                //noinspection GroovyAssignabilityCheck
                artifact androidSourcesJar
                //noinspection GroovyAssignabilityCheck
                artifact androidJavadocsJar
            }
        }
    }
}

task androidJavadocs(type: Javadoc) {
    source += android.sourceSets.main.java.srcDirs
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

bintray {
    publications = ['kotlinSimpleXmlRssAndroid']

    pkg {
        name = 'kotlin-simplexml-rss-android'
        labels = ['kotlin', 'rss', 'simplexml', 'xml', 'android']
    }
}

spotless {
    kotlin {
        ktlint()
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation 'org.simpleframework:simple-xml:2.7.1'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    api project(':kotlin-simplexml-rss'), {
        exclude group: 'org.threeten:threetenbp'
    }
}