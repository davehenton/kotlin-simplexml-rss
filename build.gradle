buildscript {
    ext.kotlin_version = '1.2.10'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}

plugins {
    id "java"
    id "java-library"
    id "jacoco"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.0"
    id "org.shipkit.java" version "0.9.152"
    id "com.diffplug.gradle.spotless" version "3.7.0"
}

apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'

group 'com.github.magneticflux'

repositories {
    jcenter()
    maven { url "http://dl.bintray.com/jetbrains/spek" }
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

publishing {
    publications {
        myPublication(MavenPublication) {
            from components.java
            //noinspection GroovyAssignabilityCheck
            artifact sourcesJar
            //noinspection GroovyAssignabilityCheck
            artifact javadocJar
        }
    }
}

spotless {
    kotlin {
        ktlint()
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile 'org.simpleframework:simple-xml:2.7.1'
    compile 'org.threeten:threetenbp:1.3.6'

    testCompile 'com.natpryce:hamkrest:1.4.2.2'
    testCompile 'org.jetbrains.spek:spek-api:1.1.5'
    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.5'
}

jacoco {
    applyTo junitPlatformTest
}

junitPlatformTest {
    jacoco {
        destinationFile = file("$buildDir/jacoco/test.exec")
    }
}

jacocoTestReport {
    executionData junitPlatformTest
    sourceSets sourceSets.main
    dependsOn(junitPlatformTest)

    reports {
        //noinspection GroovyAssignabilityCheck
        xml {
            enabled true
        }
    }
}
check.dependsOn(jacocoTestReport)