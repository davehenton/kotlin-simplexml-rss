buildscript {
    ext.kotlin_version = '1.2.20'
    ext.spek_version = '1.1.5'

    repositories {
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.16-eap-2"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.1.0-M1'
    }
}

plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.0'
    id 'org.shipkit.java' version '1.0.7'
    id 'com.diffplug.gradle.spotless' version '3.8.0'
    id 'com.github.ben-manes.versions' version '0.17.0'
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'org.junit.platform.gradle.plugin'

group 'com.github.magneticflux'

repositories {
    jcenter()
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    maven { url 'http://dl.bintray.com/jetbrains/spek' }
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

publishing {
    publications {
        myPublication(MavenPublication) {
            from components.java
            //noinspection GroovyAssignabilityCheck
            artifact sourcesJar
            //noinspection GroovyAssignabilityCheck
            artifact dokkaJavadocJar
        }
    }
}

dokka {
    outputFormat = 'javadoc'
    jdkVersion = 7
    impliedPlatforms = ["JVM"]
}

task dokkaJavadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}

spotless {
    kotlin {
        ktlint('0.14.0')
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    api 'org.simpleframework:simple-xml:2.7.1'
    api 'org.threeten:threetenbp:1.3.6'

    testImplementation 'org.hamcrest:java-hamcrest:2.0.0.0'
    testImplementation "org.jetbrains.spek:spek-api:$spek_version"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testRuntimeOnly "org.jetbrains.spek:spek-junit-platform-engine:$spek_version"
}

jacoco {
    applyTo junitPlatformTest
}

junitPlatformTest {
    jacoco {
        destinationFile = file("$buildDir/jacoco/test.exec")
    }
}

jacocoTestReport {
    executionData junitPlatformTest
    sourceSets sourceSets.main
    dependsOn(junitPlatformTest)

    reports {
        //noinspection GroovyAssignabilityCheck
        xml {
            enabled true
        }
    }
}
check.dependsOn(jacocoTestReport)